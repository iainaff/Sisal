% TWO DRIVING LOOPS: OPTIMIZED FOR POINTER SWAP
% RENUM SHIFT OPTIMIZATION
define Main

type Double = double_real;

type Oned = array[Double];
type Twod = array[Oned];
type Onei = array[integer];
type Twoi = array[Onei];

global LOG( x:Double returns Double )


function RUNKUT(COF1,COF2,COF3,COF4,COF5,COF6,RATIO:Double; 
		LN:Twod; N:integer returns Twod )
let
  rc1,rc2,rc3,rc4,rc5 :=
      for J in 2,N
          CLI      := LN[1,J];
          CMI      := LN[2,J];
          CMLI     := LN[3,J];
          CML2I    := LN[4,J];
          CML2ISOI := LN[5,J];

          RKK1 := COF1*CMI*CLI + COF2*CMLI;
          RKL1 := -(RKK1 + COF3*CMLI*CLI + COF4*CML2I);
          RKP1 := RATIO*(RKK1 + RKK1 + RKL1);
          RKM1 := COF5*CML2I + COF6*CML2ISOI;
          RKN1 := -(RKK1 + RKL1 + RKM1);

          U  := CLI + double_real(0.5e0)*RKP1;
          W  := CML2I + double_real(0.5e0)*RKN1;
          XX := CMLI + double_real(0.5e0)*RKL1;

          RKK2 := COF1*(CMI + double_real(0.5e0)*RKK1)*U + COF2*XX;
          RKL2 := -(RKK2 + COF3*XX*U + COF4*W);
          RKP2 := RATIO*(RKK2 + RKK2 + RKL2);
          RKM2 := COF5*W + COF6*(CML2ISOI + double_real(0.5e0)*RKM1);
          RKN2 := -(RKK2 + RKL2 + RKM2);

          VV := CLI + double_real(0.5e0)*RKP2;
          Y  := CMLI + double_real(0.5e0)*RKL2;
          Z  := CML2I + double_real(0.5e0)*RKN2;

          RKK3 := COF1*(CMI + double_real(0.5e0)*RKK2)*VV + COF2*Y;
          RKL3 := -(RKK3 + COF3*Y*VV + COF4*Z);
          RKP3 := RATIO*(RKK3 + RKK3 + RKL3);
          RKM3 := COF5*Z +COF6*(CML2ISOI + double_real(0.5e0)*RKM2);
          RKN3 := -(RKK3 + RKL3 + RKM3);

          R := CLI + RKP3;
          S := CMLI + RKL3;
          T := CML2I + RKN3;

          RKK4 := COF1*(CMI + RKK3)*R + COF2*S;
          RKL4 := -(RKK4 + COF3*S*R + COF4*T);
          RKP4 := RATIO*(RKK4 + RKK4 + RKL4);
          RKM4 := COF5*T + COF6*(CML2ISOI + RKM3);
          RKN4 := -(RKK4 + RKL4 + RKM4);

          DELK := (RKK1 + RKK2 + RKK2 + RKK3 + RKK3 + RKK4)/double_real(6.0e0);
          DELL := (RKL1 + RKL2 + RKL2 + RKL3 + RKL3 + RKL4)/double_real(6.0e0);
          DELM := (RKM1 + RKM2 + RKM2 + RKM3 + RKM3 + RKM4)/double_real(6.0e0);

          v1 := CLI + RATIO*(DELK + DELK + DELL);
          v2 := CMI + DELK;
          v3 := CMLI + DELL;
          v4 := CML2I - (DELK + DELL + DELM);
          v5 := CML2ISOI + DELM;
      returns array of v1
              array of v2
              array of v3
              array of v4
              array of v5
      end for;

  r1 := array_addl(rc1,double_real(0.0e0));
  r2 := array_addl(rc2,double_real(0.0e0));
  r3 := array_addl(rc3,double_real(0.0e0));
  r4 := array_addl(rc4,double_real(0.0e0));
  r5 := array_addl(rc5,double_real(0.0e0));
in
  array [1:r1, r2, r3, r4, r5 ]
end let
end function


function RENUM( NP:integer; LN:Twod; N,I,IELUTE:integer; VSEG:Double;
		CELUTE:Twod returns Twod, Double )
let
  K := I/IELUTE;
  VOL := double_real(K)*VSEG;

  CELUTE_1 := CELUTE[1,K: LN[1,N];
		     2,K: LN[2,N];
		     3,K: LN[3,N];
		     4,K: LN[4,N];
		     5,K: LN[5,N]  ];
in
  CELUTE_1, VOL
end let
end function


function OUT( LN:Twod; N:integer; VOL:Double; KELUTE:integer;
	      VSEG,F:Double; CELUTE:Twod 
	      returns Double, Oned, Oned, Double, Double,
		      Double, Double, Double, Double, Double, 
		      integer, Double, Double, Double )
let
  CTL,
  CTM := for J in 1,KELUTE
           CTL := CELUTE[1,J] + CELUTE[3,J] + CELUTE[4,J] +
                  CELUTE[4,J] + CELUTE[5,J] + CELUTE[5,J];
           CTM := CELUTE[2,J] + CELUTE[3,J] + CELUTE[4,J] +
                  CELUTE[5,J];
	 returns array of CTL
		 array of CTM
         end for;

  TOTM,
  TOTML,
  TOTML2,
  TOTML2I := for J in 1,KELUTE
             returns value of sum CELUTE[2,J]
                     value of sum CELUTE[3,J]
                     value of sum CELUTE[4,J]
                     value of sum CELUTE[5,J]
             end for; 

  TOT := TOTM + TOTML + TOTML2 + TOTML2I;
  TOTMA := double_real(1.554870369e-05) * double_real(0.486e0);

  PERML,
  JSTOR,
  STOR,
  PERCENT,
  HL := if (TOT = double_real(0.0e0)) then
	  double_real(0.0e0), 0, double_real(0.0e0), double_real(0.0e0), double_real(0.0e0)
        else
	  let
            PERML := double_real(100.0e0) * (TOTML + TOTML2 + TOTML2I) / TOT;

	    STOR,
	    JSTOR := for initial
                       STOR := CTL[915];
                       JSTOR := 915;
		       J := 915;
                     while ( J <= 1190 ) repeat
		       J := old J + 1;

                       STOR,
	               JSTOR := if (old STOR<CTL[old J]) then 
		                  old STOR, old JSTOR
	                        else
                                  CTL[old J], old J
	                        end if;
		     returns value of STOR
			     value of JSTOR
                     end for;


	    TOT1 := for J in 1,KELUTE returns value of sum CTL[J] end for;
	    TOT2 := for J in 1,JSTOR returns value of sum CTL[J] end for;
            PERCENT := double_real(100.0e0)*TOT2/TOT1;
            HL :=-LOG(double_real(2.0e0))*double_real(JSTOR)*
		 double_real(0.016e0)/(F*LOG(PERCENT/double_real(18.02233e0)));
	  in
            PERML, JSTOR, STOR, PERCENT, HL
	  end let
        end if;
in
  VOL, CTM, CTL, TOTM, TOTML, TOTML2, TOTML2I, TOT, PERML, TOTMA, JSTOR,
  STOR, PERCENT, HL
end let
end function


function FILLUP( N:integer; DX:Double; KELUTE:integer; VSEG:Double;
		 NSEG:integer; GZERO:Oned returns Oned, Twod, Twod, Oned )
let
  X_c := for J in 2,N returns array of double_real(J-1)*DX end for;
  X   := array_addl( X_c, double_real(0.0e0) );
  V   := for J in 1,KELUTE returns array of double_real(J)*VSEG end for;

  C   := for M in 1,5
	   Cr := array_fill(2,NSEG,GZERO[M]) || array_fill(NSEG+1,N,double_real(0.0e0));
	 returns array of array_addl(Cr,double_real(0.0e0))
	 end for;

  CELUTE := for I in 1,5 cross J in 1,KELUTE returns array of double_real(0.0e0) end for;
in
  X, C, CELUTE, V
end let
end function


	       
function Main( returns Double, Oned, Oned, Double, Double,
		       Double, Double, Double, Double,
		       Double, integer, Double, Double, Double )
let
  NV := 40000;
  % NV := 400;

  N := 1315;
  NP := N-1;
  NSEG  :=  46;
  KELUTE := 2000;
  IELUTE := 20;
  DX := double_real(0.02e0);

  XI := array[1: double_real(0.8e0), double_real(0.54e0), double_real(0.54e0), double_real(0.54e0), double_real(0.54e0) ];

  RATIO  :=  XI[2] / XI[1];
  DV := XI[1]*DX/double_real(IELUTE);

  AXDIS_1 := array[1: double_real(2.0e-02), double_real(6.0e-02), double_real(6.0e-02), double_real(6.0e-02), double_real(6.0e-02) ];

  VEL,
  LAMBDA := for M in 1,5
              VEL_1  := (double_real(1.0e0)/XI[M])-(double_real(1.0e0)/XI[1]);
              AXDIS  := AXDIS_1[M]-double_real(0.5e0)*VEL_1*DX;
              VEL    := DV*VEL_1/DX;
              LAMBDA := DV*AXDIS/(exp(DX,double_real(2.0e0)));
	    returns array of VEL
		    array of LAMBDA
            end for;


  VSEG := XI[1]*DX;
  F  :=  double_real(20.6e0) / double_real(3600.0e0);
  EK0  :=  double_real(1.1e05);
  RDML  :=  double_real(0.5e0);
  RAML  :=  double_real(2.0e0)*EK0*RDML;
  EKISOM  :=  double_real(20.0e0);
  EKAPP  :=  double_real(0.5e0)*EK0/(double_real(1.0e0) + EKISOM);
  RDML2  :=  double_real(1.0e0);
  RAML2  :=  EKAPP*RDML2;
  RRISOM  :=  double_real(1.0e-03);
  RFISOM  :=  EKISOM*RRISOM;
  COF1  :=  -RAML * DV / F;
  COF2  :=  RDML * DV / F;
  COF3  :=  RAML2 * DV / F;
  COF4  :=  -RDML2 * DV / F;
  COF5  :=  RFISOM * DV / F;
  COF6  :=  -RRISOM * DV / F;

  GZERO := array[1: double_real(0.4038637706e-05), double_real(0.7454342552e-05), double_real(0.6623185565e-05),
                    double_real(0.0070055980e-05), double_real(0.1401119600e-05) ];

  VOL,
  CELUTE,
  LN := for initial
          VOL := double_real(0.0e0);
	  I := 1; S := 0;
          X, LN, CELUTE, V := FILLUP(N,DX,KELUTE,VSEG,NSEG,GZERO);
	  
        while ( I <= NV ) repeat
	  I := old I + 1;

	  LN1 := for M in 1,5
		   Sp2 := old S+2; Sp3 := old S+3; SpN := old S+N;

		   LNrc := for JJ in 3, NP
			     J := JJ + old S;
                             V := old LN[M,J]+LAMBDA[M]*(old LN[M,J+1]-
				  old LN[M,J]-old LN[M,J]+old LN[M,J-1])-
				  VEL[M]*(old LN[M,J]-old LN[M,J-1]);
			   returns array of V
			   end for;

                  LNc2 := old LN[M,Sp2]+LAMBDA[M]*
			  (old LN[M,Sp3]-old LN[M,Sp2])-VEL[M]*old LN[M,Sp2];
                  LNn  := old LN[M,SpN]+LAMBDA[M]*
			  (old LN[M,SpN-1]-old LN[M,SpN]);

		  LNr  := array_addl(array_addh(LNrc,LNn),LNc2);
		  returns array of array_addl(LNr,double_real(0.0e0))
		  end for;

          LN := RUNKUT(COF1,COF2,COF3,COF4,COF5,COF6,RATIO,LN1,N);

	  S, CELUTE, VOL := if ((old I/IELUTE)*IELUTE = old I) then
		              -1, RENUM(NP,LN,N,old I,IELUTE,VSEG,old CELUTE)
                            else
		              0, old CELUTE, old VOL
                            end if;
        returns value of VOL
	        value of CELUTE
	        value of LN
        end for;
in
  OUT(LN,N,VOL,KELUTE,VSEG,F,CELUTE)
end let
end function
